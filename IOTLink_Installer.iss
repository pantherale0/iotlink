; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#include <idp.iss>

#define APP_NAME     "IOT Link"
#define APP_DIR_NAME "IOTLink"
#define APP_EXE_NAME "IOTLink.exe"

#define APP_AUTHOR_NAME "Alexandre Leites"
#define APP_AUTHOR_URL  "https://alexslx.com"

[Setup]
; Basic Information
AppId={{CD785E2E-5102-4053-A1E1-208CA1D8DC98}
AppName={#APP_NAME}
AppVersion=0.1.0
; Publisher Information
AppPublisher={#APP_AUTHOR_NAME}
AppPublisherURL={#APP_AUTHOR_URL}
AppSupportURL={#APP_AUTHOR_URL}
AppUpdatesURL={#APP_AUTHOR_URL}
; Installation Directort Settings
DefaultDirName={commonpf}\{#APP_DIR_NAME}
DefaultGroupName={#APP_NAME}
UsePreviousAppDir=yes
; Setup Settings
OutputDir=Setup
OutputBaseFilename={#APP_DIR_NAME}_InstallerWizardStyle=modern
AllowNoIcons=yes
LicenseFile=LICENSE
; Compression
Compression=lzma2
SolidCompression=no
; Privileges Settings
PrivilegesRequired=admin


[Files]
Source: "IOTLink\bin\Release\IOTLink.exe";               DestDir: "{app}"; DestName: "{#APP_EXE_NAME}"; Flags: ignoreversion
Source: "IOTLink\bin\Release\MQTTnet.dll";               DestDir: "{app}"; Flags: ignoreversion
Source: "IOTLink\bin\Release\Newtonsoft.Json.dll";       DestDir: "{app}"; Flags: ignoreversion
Source: "IOTLink\bin\Release\IOTLink.exe.config";        DestDir: "{app}"; DestName: "{#APP_EXE_NAME}.config"; Flags: ignoreversion
Source: "IOTLink\bin\Release\YamlDotNet.dll";            DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "Setup\config.yaml-sample"; DestDir: "{commonappdata}\{#APP_DIR_NAME}\Configs"; DestName: "configuration.yaml"; Flags: confirmoverwrite createallsubdirs recursesubdirs

[Icons]
; Service Install/Uninstall
Name: "{group}\Install Windows Service";      Filename: "{app}\{#APP_EXE_NAME}"; WorkingDir: "{app}"; Parameters: "--install";      AfterInstall: SetElevationBit('{group}\Install Windows Service.lnk')
Name: "{group}\Uninstall Windows Service";    Filename: "{app}\{#APP_EXE_NAME}"; WorkingDir: "{app}"; Parameters: "--uninstall";    AfterInstall: SetElevationBit('{group}\Uninstall Windows Service.lnk')
; Service Start/Stop
Name: "{group}\Start Windows Service";        Filename: "net.exe"; WorkingDir: "{sys}"; Parameters: "start winiotlink";    Comment: "Start {#APP_NAME} Service";    AfterInstall: SetElevationBit('{group}\Start Windows Service.lnk')
Name: "{group}\Stop Windows Service";         Filename: "net.exe"; WorkingDir: "{sys}"; Parameters: "stop winiotlink";     Comment: "Stop {#APP_NAME} Service";     AfterInstall: SetElevationBit('{group}\Stop Windows Service.lnk')
; Open Folders
Name: "{group}\Open Addons Folder";           Filename: "{commonappdata}\{#APP_DIR_NAME}\Addons";     WorkingDir: "{commonappdata}\{#APP_DIR_NAME}\Addons";    Flags: foldershortcut
Name: "{group}\Open Configuration Folder";    Filename: "{commonappdata}\{#APP_DIR_NAME}\Configs";    WorkingDir: "{commonappdata}\{#APP_DIR_NAME}\Configs";   Flags: foldershortcut
Name: "{group}\Open Logs Folder";             Filename: "{commonappdata}\{#APP_DIR_NAME}\Logs";       WorkingDir: "{commonappdata}\{#APP_DIR_NAME}\Logs";      Flags: foldershortcut

[Run]
Filename: "{app}\{#APP_EXE_NAME}"; Parameters: "--install";      WorkingDir: "{app}"; Flags: postinstall runhidden; Description: "Install {#APP_NAME} as Service"; StatusMsg: "Installing Windows Service"

[UninstallRun]
Filename: "{app}\{#APP_EXE_NAME}"; Parameters: "--uninstall";    WorkingDir: "{app}"; Flags: runhidden

[Dirs]
Name: "{commonappdata}\{#APP_DIR_NAME}"
Name: "{commonappdata}\{#APP_DIR_NAME}\Configs"
Name: "{commonappdata}\{#APP_DIR_NAME}\Logs"
Name: "{commonappdata}\{#APP_DIR_NAME}\Addons"

[Code]
procedure SetElevationBit(Filename: string);
var
  Buffer: string;
  Stream: TStream;
begin
  Filename := ExpandConstant(Filename);
  Log('Setting elevation bit for ' + Filename);

  Stream := TFileStream.Create(FileName, fmOpenReadWrite);
  try
    Stream.Seek(21, soFromBeginning);
    SetLength(Buffer, 1);
    Stream.ReadBuffer(Buffer, 1);
    Buffer[1] := Chr(Ord(Buffer[1]) or $20);
    Stream.Seek(-1, soFromCurrent);
    Stream.WriteBuffer(Buffer, 1);
  finally
    Stream.Free;
  end;
end;

function DotNetFramework472IsNotInstalled(): Boolean;
var
  bSuccess: Boolean;
  regVersion: Cardinal;
begin
  Result := True;
  bSuccess := RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', regVersion);
  if (True = bSuccess) and (regVersion >= 461808) then begin
    Result := False;
  end;
end;

procedure InitializeWizard;
begin
  if DotNetFramework472IsNotInstalled() then
  begin
    idpAddFile('http://go.microsoft.com/fwlink/?linkid=863265', ExpandConstant('{tmp}\DotNetFrameworkInstaller.exe'));
    idpDownloadAfter(wpReady);
  end;
end;

procedure InstallDotNetFramework;
var
  StatusText: string;
  ResultCode: Integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Installing .NET Framework 4.7.2. This might take a few minutes...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\DotNetFrameworkInstaller.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.', mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;
    DeleteFile(ExpandConstant('{tmp}\DotNetFrameworkInstaller.exe'));
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  case CurStep of
    ssPostInstall:
      begin
        if DotNetFramework472IsNotInstalled() then
        begin
          InstallDotNetFramework();
        end;
      end;
  end;
end;

procedure CurPageChanged(CurPageID: Integer);
begin
  if CurPageID = wpFinished then
    WizardForm.RunList.Visible := False;
end;
